@model<probabilistic>

def main()
{
  call tritrophic(10,7);
}


def tritrophic(N,M) /* N entornos y M especies */
{
  /***********************************/
  /* Estructura inicial de membranas */
  /***********************************/
  /* Tenemos 10 entornos 101..110, los entornos se representan
     en P-Lingua como membranas especiales con dos identificadores,
     el primero es la etiqueta de la membrana y el segundo es la etiqueta
     del entorno, por ejemplo: 101,101. Todas las membranas dentro
     de un entorno determinado heredan el mismo id de entorno.
     El esqueleto es [[]'1]'0, cada uno de los 10 esqueletos
     se encuentra en un entorno diferente */
  
  @mu= [
           [[[]'1]'0]'101,101  
           [[[]'1]'0]'102,102 
           [[[]'1]'0]'103,103
           [[[]'1]'0]'104,104  
           [[[]'1]'0]'105,105 
           [[[]'1]'0]'106,106
           [[[]'1]'0]'107,107  
           [[[]'1]'0]'108,108 
           [[[]'1]'0]'109,109
           [[[]'1]'0]'110,110
        ]'p;



  /****************************/
  /* Multiconjuntos iniciales */
  /****************************/

  @ms(1,{j+100}) += X{k} * q{k,j} : 2<=k<=M,1<=j<=N; /* Herbivoros y carnivoros */
  @ms(0,{j+100}) += X{1} * q{1,j} : 1<=j<=N;         /* Hierba */
  @ms(0,{j+100}) += R{0}          : 1<=j<=N;         /* Objeto contador */

  /****************************/
  /* Conjunto de reglas       */
  /****************************/

  /**********************/
  /*      Modulo 1      */
  /**********************/

  /*r1*/ X{1}[]'1-->+[X{1},G*(h{j})]'1:: m{j}: 1<=j<=3;   /* Producción de hierba */

  /*r2*/ [X{i}]'1-->+[X{i}*(1+d{i})]'1:: (k{i,1}*0.5): 2<=i<=M; /* Animales que se reproducen */
	
  /*r3*/ [X{i}]'1-->+[X{i}]'1:: 1-(k{i,1}*0.5): 2<=i<=M; /* Animales que no se reproducen */
	
  /*r4*/ R{0}[]'1-->+[R{0}]'1::1; /* Sincronización */

  /**********************/
  /*    Modulo 2        */
  /**********************/
	
  /*r5*/ +[X{i}, G*(f{i})]'1 --> -[Y{i}]'1 :: (1-k{i,2}) : 2<=i<=M-1; /* Herbívoros que se alimentan */

  /*r6*/ +[X{M}, X{i}*(f{M})]'1 --> -[Y{M}]'1 :: (1-k{M,2}) : 2<=i<=M-1; /* Carnívoros que se alimentan */
	
  /*r5*/ +[X{i}, G*(f{i})]'1 --> -[]'1 :: k{i,2} : 2<=i<=M-1; /* Herbívoros que se alimentan y mueren */

  /*r6*/ +[X{M}, X{i}*(f{M})]'1 --> -[]'1 :: k{M,2} : 2<=i<=M-1; /* Carnívoros que se alimentan y mueren */
	
  /*r9*/ +[R{0}]'1-->-[R{0}]'1::1; /* Sincronización */
	
  /*****************/
  /*   Modulo 3    */
  /*****************/
	
 /*r10*/ -[X{i}]'1 --> X{i} -[]'1 ::1 : 2<=i<=M; /* Los animales salen a la membrana piel */
	
 /*r11*/ [X{i}]'0 --> X{i} []'0 :: 1  : 2<=i<=M; /* Los animales salen al entorno */
	 
 /*re12*/ [[X{i}]'{k+100} [#]'{r+100}]'p --> 
          [[#]'{k+100} [Xp{i}]'{r+100}]'p ::p{k,r,i}: 1<=k<=N, 1<=r<=N, 2<=i<=M,k<>r; /* Desplazamiento entre entornos */
          
          [[X{i}]'{k+100}]'p --> 
          [[Xp{i}]'{k+100}]'p ::p{k,k,i} :1<=k<=N, 2<=i<=M; /* Permanencia en el mismo entorno */	 


  /*r13*/ Xp{i} []'0-->[Xp{i}]'0 :: 1 : 2<=i<=M; /* Los animales entran en la membrana piel */
	
  /*r14*/ Xp{i} -[]'1 --> -[Xp{i}]'1 :: 1 : 2<=i<=M; /* Los animales entran en la membrana 1 */
	
  /*r15*/ -[R{i}]'1--> -[R{i+1}]'1 :: 1 : 0<=i<=4;
	

  /*****************/
  /*   Modulo 3    */
  /*****************/	

  /*r16*/ -[Xp{i}, G*(f{i})]'1 --> [Y{i}]'1 :: (1-k{i,2}) : 2<=i<=M-1; /* Herbívoros que se alimentan */
	
  /*r17*/ -[Xp{M}, Xp{i}*(f{M})]'1 --> [Y{M}]'1 :: (1-k{M,2}) : 2<=i<=M-1; /* Carnívoros que se alimentan */
	
  /*r18*/ -[Xp{M}, Y{i}*(f{M})]'1 --> [Y{M}]'1 :: (1-k{M,2}) : 2<=i<=M-1; /* Carnívoros que se alimentan */
	
  /*r19*/ -[Xp{i}, G*(f{i})]'1 --> [#]'1 :: k{i,2}  : 2<=i<=M-1; /* Herbívoros que se alimentan y mueren */
	
  /*r20*/ -[Xp{M}, Xp{i}*(f{M})]'1 --> [#]'1 :: k{M,2} : 2<=i<=M-1; /* Carnívoros que se alimentan y mueren */
	
  /*r21*/ -[Xp{M}, Y{i}*(f{M})]'1 --> [#]'1 :: k{M,2}  : 2<=i<=M-1; /* Carnívoros que se alimentan y mueren */
	
  /*r22*/ -[R{5}]'1-->[R{6}]'1::1; /* Sincronización */
	
	
 /************/
 /* Modulo 5 */
 /************/
  /*r23*/[Y{i}]'1-->[X{i}]'1:: 1 : 2<=i<=M;
	
  /*r24*/[R{6}]'1-->[R{0}]'1::1;
	
  /*r25*/ [X{1}]'1-->X{1}[#]'1::1;
	
  /*r26*/ [Xp{i}]'1-->[#]'1 :: 1 : 2<=i<=M;
	
  /*r27*/ [G]'1-->[#]'1::1;
	

}
